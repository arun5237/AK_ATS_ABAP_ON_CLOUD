unmanaged implementation in class zbp_ats_ak_um_travel unique;
strict ( 2 );

define behavior for ZATS_AK_UM_TRAVEL alias Travel
//late numbering
lock master
authorization master ( instance )
//etag master <field_name>
{
  field ( readonly : update ) TravelId; // Field TravelId is non-editable in update mode
  field ( mandatory ) AgencyId, CustomerId, BeginDate, EndDate;
  create;
  update;
  delete;
  action ( features : instance ) set_booked_status result [1] $self; //Custom action to calculate and set Booking Status

  // We need to know in the code which fields are changed on the screen by the user so that we can update the data
  //  into the database only for those changed fields.
  // RAP provides a compiler structure called %control which will tell us what fields were changed on UI.
  //  We need to tell the RAP, what will be the datatype for the control structure using below statement.
  // Check /dmo/s_travel_intx. All fields are boolean indicating that field is changed or not
  mapping for /dmo/travel control /dmo/s_travel_intx
    {
      TravelId      = travel_id;
      AgencyId      = agency_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      CustomerId    = customer_id;
      CurrencyCode  = currency_code;
      BookingFee    = booking_fee;
      TotalPrice    = total_price;
      Status        = status;
      Lastchangedat = lastchangedat;
      Createdat     = createdat;
      Memo          = description;
    }

}