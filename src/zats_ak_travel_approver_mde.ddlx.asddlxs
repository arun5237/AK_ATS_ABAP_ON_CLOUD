@Metadata.layer: #CUSTOMER
@UI.headerInfo: {
typeName: 'Travel',
typeNamePlural: 'Travels',
title: { value: 'TravelId' },
description: { value: '_Customer.FirstName' }
}
annotate view ZATS_AK_TRAVEL_APPROVER with
{
  @UI.facet: [
  {
  purpose: #HEADER,
  type: #DATAPOINT_REFERENCE,
  label: 'Flight Ticket Price',
  position: 10,
  targetQualifier: 'price_data'
   },
   {
   purpose: #HEADER,
   type: #DATAPOINT_REFERENCE,
   label: 'Status',
   position: 20,
   targetQualifier: 'status'
   },
   {
   purpose: #STANDARD,
   type: #COLLECTION,
   position: 10,
   id: 'super',
   label: 'Additional Details'
   },
   {
   purpose: #STANDARD,
   type: #IDENTIFICATION_REFERENCE,
   label: 'More Info',
   position: 10,
   parentId: 'super'
   },
   {
   purpose: #STANDARD,
   type: #FIELDGROUP_REFERENCE,
   label: 'Price data',
   position: 20,
   parentId: 'super',
   targetQualifier: 'pricing'
   },
   {
   purpose: #STANDARD,
   type: #FIELDGROUP_REFERENCE,
   label: 'Dates',
   position: 30,
   parentId: 'super',
   targetQualifier: 'dates'
   },
   {
   purpose: #STANDARD,
   type: #LINEITEM_REFERENCE,
   label: 'Bookings',
   position: 20,
   targetElement: '_Booking'
   },
  {
   purpose: #STANDARD,
   type: #FIELDGROUP_REFERENCE,
   label: 'Admin Information',
   position: 40,
   parentId: 'super',
   targetQualifier: 'admin'
   }
   ]
  @UI.selectionField: [{ position: 10 }]
  @UI.lineItem: [{ position: 10 }]
  @UI.identification: [{ position: 10 }]
  TravelId;
  @UI.selectionField: [{ position: 20 }]
  @UI.lineItem: [{ position: 20 }]
  @UI.identification: [{ position: 20 }]
  AgencyId;
  @UI.selectionField: [{ position: 30 }]
  @UI.lineItem: [{ position: 30 }]
  @UI.identification: [{ position: 30 }]
  CustomerId;
  @UI.lineItem: [{ position: 40 }]
  @UI.fieldGroup: [{ qualifier: 'dates', label: 'Begin Date', position: 10 }]
  BeginDate;
  @UI.fieldGroup: [{ qualifier: 'dates', label: 'End Date', position: 20 }]
  EndDate;
  @UI.fieldGroup: [{ qualifier: 'pricing', label: 'Booking Fee', position: 10 }]
  BookingFee;
  @UI.selectionField: [{ position: 40 }]
  @UI.lineItem: [{ position: 50 }]
  @UI.dataPoint: { qualifier: 'price_data', title: 'Flight Price' } //This will link the targetQualifier specified in UI.facet above
  @UI.fieldGroup: [{ qualifier: 'pricing', label: 'Total', position: 20 }]
  TotalPrice;
  @UI.fieldGroup: [{ qualifier: 'pricing', label: 'Currency Code', position: 30 }]
  CurrencyCode;
  //  Description;
  @UI.selectionField: [{ position: 50 }]
  @UI.lineItem: [{ position: 60, criticality: 'Criticality', // Criticality annotation points to field Criticality to give colors,
  importance: #HIGH }, // Importance ensures that field is displayed madatorily in list view
  { type: #FOR_ACTION, label:'Approve Request',
    dataAction: 'acceptTravel' },
    { type: #FOR_ACTION, label:'Reject Request',
    dataAction: 'rejectTravel' }]
  @UI.identification: [
   { type: #FOR_ACTION, label:'Accept Travel',
    dataAction: 'acceptTravel' },
    { type: #FOR_ACTION, label:'Reject Travel',
    dataAction: 'rejectTravel' }]
  @UI.dataPoint: { title: 'Travel Status', qualifier: 'status', criticality: 'Criticality' }
  @UI.fieldGroup: [{ qualifier: 'dates', label: 'Status', position: 30 }]
  OverallStatus;
}